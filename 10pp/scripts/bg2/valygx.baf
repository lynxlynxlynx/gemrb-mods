IF
  Delay(10)
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_INVISIBLE)
THEN
  RESPONSE #100
    Hide()
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_ARMOR_OF_FAITH)
  CheckStatGT(Myself,3,ARMORCLASS)
  HPGT(LastSeenBy(Myself),20)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_ARMOR_OF_FAITH)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_BLESS)
  HPGT(LastSeenBy(Myself),20)
  !StateCheck(LastSeenBy(Myself),STATE_BLESS)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_BLESS)
    Continue()
END

IF
  OR(4)
    StateCheck(Myself,STATE_PANIC)
    SpellCast([ANYONE],WIZARD_HORROR)
    SpellCast([ANYONE],WIZARD_SPOOK)
    SpellCastPriest([ANYONE],CLERIC_CLOAK_OF_FEAR)
  HaveSpell(CLERIC_REMOVE_FEAR)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_REMOVE_FEAR)
    Continue()
END

IF
  See(NearestEnemyOfType([0.ANIMAL]))
  HaveSpell(RANGER_CHARM_ANIMAL)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),RANGER_CHARM_ANIMAL)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_HASTED)
  HaveSpell(WIZARD_HASTE)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_HASTE)
    Continue()
END

IF
  HitBy([ANYONE],MISSILE)
  HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
  !HasImmunityEffects(Myself)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
    Continue()
END

IF
  HaveSpell(WIZARD_MINOR_SPELL_DEFLECTION)
  OR(4)
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([0.0.0.BARD_ALL]))
    See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
  !HasBounceEffects(Myself)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_MINOR_SPELL_DEFLECTION)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  !Range(LastSeenBy(Myself),20)
  HaveSpell(CLERIC_ENTANGLE)
  AreaCheck("UTDOOR)")
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_ENTANGLE)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_DOOM)
THEN
  RESPONSE #50
    Spell(LastSeenBy(Myself),CLERIC_DOOM)
    Continue()
  RESPONSE #50
    Continue()
END

IF
  HPPercentLT(Myself,30)
  HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
THEN
  RESPONSE #100
    Spell(MostDamagedOf(Myself),CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
  HPPercentLT(Myself,60)
  HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
THEN
  RESPONSE #100
    Spell(MostDamagedOf(Myself),CLERIC_CURE_LIGHT_WOUNDS)
END

IF
  AttackedBy([ANYONE],DEFAULT)
  Range(LastAttackerOf(Myself),20)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackReevaluate(LastAttackerOf(Myself),30)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),10)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackReevaluate(NearestEnemyOf(Myself),30)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  !Range(NearestEnemyOf(Myself),10)
THEN
  RESPONSE #100
    EquipRanged()
    AttackReevaluate(NearestEnemyOf(Myself),30)
    Continue()
END

