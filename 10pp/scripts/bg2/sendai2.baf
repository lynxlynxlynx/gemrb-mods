IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  Global("Preparation","LOCALS",0)
THEN
  RESPONSE #100
    ApplySpell(Myself,CLERIC_REGENERATE)
    ApplySpell(Myself,CLERIC_DEATH_WARD)
    ApplySpell(Myself,CLERIC_IRONSKIN)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_DEFENSIVE_HARMONY)
    SetGlobal("Preparation","LOCALS",1)
END

IF
  !GlobalTimerNotExpired("TrueSeeing","LOCALS")
  Detect(NearestEnemyOf(Myself))
  OR(12)
    StateCheck(NearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    SetGlobalTimer("TrueSeeing","LOCALS",60)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(NPC_BLADE_BARRIER)
  RandomNum(2,1)
  Global("BladeBarrier","LOCALS",0)
THEN
  RESPONSE #100
    Spell(Myself,NPC_BLADE_BARRIER)
    SetGlobal("BladeBarrier","LOCALS",1)
END

IF
  See([GOODCUTOFF.0.SKELETON.0.0.SUMMONED])
  HaveSpell(CLERIC_SUNRAY)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_SUNRAY)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,1)
  HaveSpell(CLERIC_CREEPING_DOOM)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CREEPING_DOOM)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_FREE_ACTION)
  See(NearestMyGroupOfType)
  StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_FREE_ACTION)
END

IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  HaveSpell(CLERIC_MASS_CURE)
  HPPercentLT(Myself,80)
  OR(3)
    HPPercentLT(LastSeenBy(Myself),80)
    HPPercentLT(SecondNearestMyGroupOfType,80)
    HPPercentLT(ThirdNearestMyGroupOfType,80)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_MASS_CURE)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,1)
  HaveSpell(CLERIC_FINGER_OF_DEATH)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_FINGER_OF_DEATH)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,2)
  HaveSpell(CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,1)
  HaveSpell(CLERIC_CONFUSION)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CONFUSION)
END

IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  HPPercentLT(LastSeenBy(Myself),10)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_HEAL)
END

IF
  See(NearestEnemyOf(Myself))
  !Range(LastSeenBy(Myself),4)
  HPPercentLT(Myself,10)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_HEAL)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_HEAL)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_CURE_CRITICAL_WOUNDS)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_CURE_CRITICAL_WOUNDS)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,1)
  HaveSpell(CLERIC_CONJURE_FIRE_ELEMENTAL)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,2)
  HaveSpell(CLERIC_CONJURE_EARTH_ELEMENTAL)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CONJURE_EARTH_ELEMENTAL)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(2,1)
  HaveSpell(CLERIC_AERIAL_SERVANT)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_AERIAL_SERVANT)
END

IF
  HaveSpell(CLERIC_SILENCE_15_FOOT)
  OR(2)
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_FLAME_STRIKE)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_FLAME_STRIKE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_HOLD_PERSON)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_HOLD_PERSON)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CLOAK_OF_FEAR)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CLOAK_OF_FEAR)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_PHYSICAL_MIRROR)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_PHYSICAL_MIRROR)
END

IF
  See(NearestEnemyOf(Myself))
  !Range(NearestEnemyOf(Myself),7)
THEN
  RESPONSE #100
    EquipRanged()
    AttackReevaluate(NearestEnemyOf(Myself),45)
END

IF
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),7)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackReevaluate(NearestEnemyOf(Myself),45)
END

