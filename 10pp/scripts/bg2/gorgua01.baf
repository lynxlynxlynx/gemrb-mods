IF
  Die()
THEN
  RESPONSE #100
    ReallyForceSpellDead(Myself,ILLUSION_DEATH)
    Wait(1)
    DestroySelf()
END

IF
  HPPercentLT(Myself,30)
  !GlobalTimerNotExpired("AuruHealTimer","LOCALS")
  !GlobalLT("AuruHeal","LOCALS",3)
THEN
  RESPONSE #100
    SetGlobalTimer("AuruHealTimer","LOCALS",12)
    IncrementGlobal("AuruHeal","LOCALS",1)
    ForceSpell(Myself,CLERIC_HEAL)
    ApplySpell(Myself,0)
    ApplySpell(Myself,CLERIC_LESSER_RESTORATION)
END

IF
  Global("AuruTimeStop","LOCALS",0)
  RandomNum(2,1)
THEN
  RESPONSE #100
    SetGlobal("AuruTimeStop","LOCALS",1)
    ForceSpell(Myself,WIZARD_TIME_STOP)
END

IF
  OR(6)
    StateCheck(NearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_INVISIBLE)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  !GlobalTimerNotExpired("AuruSight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("AuruSight","LOCALS",60)
    ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
END

IF
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("AuruMirror","LOCALS")
  RandomNum(2,1)
THEN
  RESPONSE #100
    SetGlobalTimer("AuruMirror","LOCALS",15)
    ForceSpell(Myself,WIZARD_MIRROR_IMAGE)
END

IF
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    NoAction()
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(3)
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.BARD]))
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]))
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(3)
    See(NearestEnemyOfType([GOODCUTOFF.0.0.BARD]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(2)
    See(ThirdNearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
    See(ThirdNearestEnemyOfType([GOODCUTOFF.0.0.SORCERER]))
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(2)
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.SORCERER]))
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(2)
    See(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.SORCERER]))
THEN
  RESPONSE #100
    Continue()
END

IF
  AttackedBy([ANYONE],DEFAULT)
  See(LastAttackerOf(Myself))
THEN
  RESPONSE #100
    Continue()
END

IF
  Range(NearestEnemyOf(Myself),4)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Continue()
END

IF
  Allegiance(LastSeenBy(Myself),EVILCUTOFF)
  See([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject([GOODCUTOFF])
END

IF
  Allegiance(LastSeenBy(Myself),EVILCUTOFF)
  !See([GOODCUTOFF])
THEN
  RESPONSE #100
    NoAction()
END

IF
  Gender(LastSeenBy(Myself),SUMMONED)
  !LevelGT(LastSeenBy(Myself),10)
  !GlobalTimerNotExpired("AuruDeathSpell","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("AuruDeathSpell","LOCALS",30)
    ForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
END

IF
  RandomNum(2,1)
  Global("AuruSymbol","LOCALS",0)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    SetGlobal("AuruSymbol","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),WIZARD_NPC_SYMBOL_FEAR)
END

IF
  Range(LastSeenBy(Myself),10)
  RandomNum(3,1)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),WIZARD_CONE_OF_COLD)
END

IF
  Range(LastSeenBy(Myself),10)
  RandomNum(4,1)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),WIZARD_PRISMATIC_SPRAY)
END

IF
  RandomNum(3,1)
  NumCreatureLT("gorgua02",2)  // ~Ferrumach Rilmani~
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !GlobalTimerNotExpired("AuruDeathFog","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("AuruDeathFog","LOCALS",30)
    ForceSpell(LastSeenBy(Myself),WIZARD_DEATH_FOG)
END

IF
  RandomNum(4,1)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  !HasBounceEffects(LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),NALMISSRA_CHARM)
END

IF
  Range(LastSeenBy(Myself),10)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  RandomNum(2,1)
THEN
  RESPONSE #100
    ForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
END

IF
  !StateCheck(LastSeenBy(Myself),STATE_SLOWED)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  !HasBounceEffects(LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  RandomNum(4,1)
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),WIZARD_SLOW)
END

IF
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #50
    AttackReevaluate(LastSeenBy(Myself),45)
END

