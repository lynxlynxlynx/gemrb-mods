IF
  !Global("SmokeBomb","LOCALS",0)
  !TimerActive(50)
  CreatureHidden(Myself)
THEN
  RESPONSE #100
    SpellHitEffectSprite(Myself,Myself,CONE_OF_COLD,10)
    SmallWait(5)
    SpellHitEffectSprite(Myself,Myself,SOLS_SEARING_ORB,10)
    HideCreature(Myself,FALSE)
    Continue()
END

IF
  !Global("SmokeBomb","LOCALS",0)
  TimerActive(50)
  CreatureHidden(Myself)
THEN
  RESPONSE #100
    RandomWalk()
END

IF
  HasItem("00potn04",Myself)  // ~Potion of Healing~
  HPPercentLT(Myself,33)
THEN
  RESPONSE #100
    UseItem("00potn04",Myself)  // ~Potion of Healing~
END

IF
  Global("SmokeBomb","LOCALS",0)
  !CreatureHidden(Myself)
  !IsCreatureHiddenInShadows(Myself)
  HPPercentLT(Myself,75)
  Range(NearestEnemyOf(Myself),2,LESS_THAN)
THEN
  RESPONSE #100
    SetGlobal("SmokeBomb","LOCALS",1)
    SpellHitEffectSprite(Myself,Myself,SOLS_SEARING_ORB,10)
    SmallWait(5)
    SpellHitEffectSprite(Myself,Myself,CONE_OF_COLD,10)
    SmallWait(5)
    HideCreature(Myself,TRUE)
    ReallyForceSpell(Myself,WIZARD_SKULL_TRAP)
    StartTimer(50,7)
    RandomWalk()
END

IF
  GlobalLT("SmokeBomb","LOCALS",2)
  !CreatureHidden(Myself)
  !IsCreatureHiddenInShadows(Myself)
  HPPercentLT(Myself,50)
  Range(NearestEnemyOf(Myself),2,LESS_THAN)
THEN
  RESPONSE #100
    SetGlobal("SmokeBomb","LOCALS",2)
    SpellHitEffectSprite(Myself,Myself,COLD,0)
    HideCreature(Myself,TRUE)
    ReallyForceSpell(Myself,CLERIC_GLYPH_OF_WARDING)
    StartTimer(50,7)
    RandomWalk()
END

IF
  !StateCheck(Myself,STATE_INVISIBLE)
  IsCreatureHiddenInShadows(Myself)
  !CreatureHidden(Myself)
  Or(2)
    See(WeakestOf(Myself),0)
    See(NearestEnemyOf(Myself),0)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    BackStab(LastMarkedObject)
END

IF
  !CreatureHidden(Myself)
  Or(2)
    See(WeakestOf(Myself),0)
    See(NearestEnemyOf(Myself),0)
THEN
  RESPONSE #100
    EquipWeapon()
    AttackOneRound(LastMarkedObject)
END

